plugins {
  alias(libs.plugins.architectury.plugin)
  alias(libs.plugins.architectury.loom).apply(false)
}

architectury {
  minecraft = libs.versions.minecraft.get()
}

subprojects {
  apply plugin: libs.plugins.architectury.loom.get().pluginId

  loom {
    silentMojangMappingsLicense()
  }

  dependencies {
    minecraft(libs.minecraft)
    mappings(loom.officialMojangMappings())
    compileOnly(libs.sponge.mixin)
  }
}

allprojects {
  apply plugin: "java"
  apply plugin: libs.plugins.architectury.plugin.get().pluginId
  apply plugin: "maven-publish"

  archivesBaseName = project.mod_id
  version = libs.versions.minecraft.get() + '-' + project.mod_version
  group = project.maven_group

  repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
  }

  tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release.set(17)
  }

  java {
    withSourcesJar()
  }

  jar {
    manifest {
      attributes([
        "Specification-Title"     : "nerb",
        "Specification-Vendor"    : "SSKirillSS",
        "Specification-Version"   : "1",
        "Implementation-Title"    : project.name,
        "Implementation-Version"  : "${mod_version}",
        "Implementation-Vendor"   : "SSKirillSS",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
      ])
    }
  }
}
