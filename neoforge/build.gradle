plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"

    modImplementation "it.hurts.octostudios.octolib:octolib-neoforge:$rootProject.octolib_version"

    modImplementation "mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}"
    modImplementation "me.shedaniel:RoughlyEnoughItems-neoforge:${rei_version}"
    modImplementation "dev.emi:emi-neoforge:${emi_version}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

def resourceTargets = ['META-INF/neoforge.mods.toml']
def replaceProperties = [
        mod_version: mod_version, mod_group: mod_group, mod_name: mod_name,
        mod_id     : mod_id, mod_description: mod_description, mod_authors: mod_authors,
        mod_license: mod_license, mod_tracker: mod_tracker
]

processResources {
    inputs.properties replaceProperties

    filesMatching(resourceTargets) {
        expand replaceProperties
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile

    atAccessWideners.add("${rootProject.properties.mod_id}.accesswidener")
}
